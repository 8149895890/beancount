/* -*- mode: c++ -*- */
// Scanner definition for Beancount's DSL (with RE/flex).
//
// See RE/flex manual: https://www.genivia.com/doc/reflex/html/

%top{
#include "beancount/cparser/parser.h"    /* Generated by bison. */
#include "beancount/cparser/location.h"  /* Generated by bison %locations. */

#include <utility>
#include <cassert>

#include "reflex/matcher.h"
#include "absl/strings/string_view.h"
#include "absl/strings/str_cat.h"
#include "absl/strings/escaping.h"
#include "absl/time/civil_time.h"
#include "absl/status/statusor.h"

namespace beancount {
namespace scanner {

// Using declarations for symbols in the scanner implementation only.
using beancount::parser::Parser;
using std::endl;
using std::string;

// Obtain a string_view of the current match.
inline std::string_view view(const reflex::Matcher& m) {
  return std::string_view(m.begin(), m.size());
}

// Copy `src` to `buffer` stripping commas on the way.
void CopyNoCommas(const char* src, char* buffer, size_t num_chars);

// Convert date string in YYYY.MM.DD format to (year, month, date).
// Only the first 10 characters are inspected; the string need not be zero-terminated.
absl::StatusOr<absl::CivilDay> ParseDateFromString(const char* string);

// Convert time string to (hour, minute, second).
absl::CivilSecond ParseTimeFromString(const char* string);

}  // namespace scanner
}  // namespace beancount
}

//- Definitions ---------------------------------------------------------------

// This option generates a ascnner that works with Bison 3.2 C++ complete
// symbols, specified by %define api.value.type variant and %define
// api.token.constructor in a Bison grammar file. This option also sets option
// −−bison-cc and sets −−token-type to the parser's symbol_type, and sets
// −−token-eof to 0, assuming these options are not specified already. Combine
// this option with −−bison-locations to support the Bison %locations feature.
%option bison-complete

// This option generates a scanner that works with Bison with locations enabled.
%option bison-locations

%option bison-cc-namespace=beancount::parser
%option bison-cc-parser=Parser

// Redirect scanner errors to raising a parser-level exception. The doc says:
//
//   This suppresses the default rule that echoes all unmatched input text when
//   no rule matches. With the −−flex option, the scanner reports "scanner
//   jammed" when no rule matches by calling yyFlexLexer::LexerError("scanner
//   jammed"). Without the −−flex and −−debug options, a std::runtime exception
//   is raised by invoking AbstractLexer::lexer_error("scanner jammed"). To
//   throw a custom exception instead, use option −−exception or override the
//   virtual method lexer_error in a derived lexer class. The virtual methods
//   LexerError and lexer_error may be redefined by a user-specified derived
//   lexer class, see Inheriting Lexer/yyFlexLexer . Without the −−flex option,
//   but with the −−debug option, the default rule is suppressed without
//   invoking lexer_error to raise an exception. See also options
//   −−exception=VALUE and -S (or −−find)."
//
// Note: syntax_error is never really invoked; because we have a default
// matching rule that gobbles up invalid tokens.
%option nodefault
%option exception="beancount::parser::Parser::syntax_error(location(), \"Unknown token.\")"

// TODO(blais): Figure out if I should put the actual derived class for the
// scanner in beancount/cparser/parser.h and set it here.
%option namespace=beancount::scanner
%option lexer=Scanner

%option tabs=8

// Unicode matching is enabled with %option unicode.
%option unicode

%option indent
%option fast


// States.
%x INVALID
%x IGNORE

// Patterns for the international components of the names of accounts.
ACCOUNTTYPE     [\p{Lu}\p{Lo}][\p{L}\p{N}\-]*
ACCOUNTNAME     [\p{Lu}\p{Lo}\p{N}][\p{L}\p{N}\-]*

// Characters that may be used as flags. Make sure to include all the
// values from beancount.core.flags to allow round-trips between text
// and entries. {5307d8fa1e7b}
FLAGS           [!&#?%PSTCURM]

//- Rules ---------------------------------------------------------------------
%%

 /* Newlines matter. */
\n { return Parser::make_EOL(location()); }

/* Indentation. This makes use of RE/flex's indentation stops feature and
 * results in much tighter indentation handling. */
^\h+                      // nodent, text is aligned to current margin column
^\h+\i                    { return Parser::make_INDENT(location()); }
^\h*\j                    { return Parser::make_DEDENT(location()); }
\j                        { return Parser::make_DEDENT(location()); }

/* Whitespace. */
[ \t\r]+                  /* Ignored whitespace. */
;.*                       /* Eat up Comments, anywhere. */

 /* Characters with special meanings. */
\|		{ return Parser::make_PIPE(location()); }
@@		{ return Parser::make_ATAT(location()); }
@		{ return Parser::make_AT(location()); }
\{\{		{ return Parser::make_LCURLCURL(location()); }
\}\}		{ return Parser::make_RCURLCURL(location()); }
\{		{ return Parser::make_LCURL(location()); }
\}		{ return Parser::make_RCURL(location()); }
,		{ return Parser::make_COMMA(location()); }
\~		{ return Parser::make_TILDE(location()); }
\+		{ return Parser::make_PLUS(location()); }
\-		{ return Parser::make_MINUS(location()); }
\/		{ return Parser::make_SLASH(location()); }
\(		{ return Parser::make_LPAREN(location()); }
\)		{ return Parser::make_RPAREN(location()); }
\#		{ return Parser::make_HASH(location()); }
\*		{ return Parser::make_ASTERISK(location()); }
\:		{ return Parser::make_COLON(location()); }

{FLAGS} {
  return Parser::make_FLAG(wchr(), location());
}

 /* Keywords. */
txn		{ return Parser::make_TXN(location()); }
balance		{ return Parser::make_BALANCE(location()); }
open		{ return Parser::make_OPEN(location()); }
close		{ return Parser::make_CLOSE(location()); }
commodity	{ return Parser::make_COMMODITY(location()); }
pad		{ return Parser::make_PAD(location()); }
event		{ return Parser::make_EVENT(location()); }
query		{ return Parser::make_QUERY(location()); }
custom		{ return Parser::make_CUSTOM(location()); }
price		{ return Parser::make_PRICE(location()); }
note		{ return Parser::make_NOTE(location()); }
document	{ return Parser::make_DOCUMENT(location()); }
pushtag	        { return Parser::make_PUSHTAG(location()); }
poptag		{ return Parser::make_POPTAG(location()); }
pushmeta	{ return Parser::make_PUSHMETA(location()); }
popmeta		{ return Parser::make_POPMETA(location()); }
option		{ return Parser::make_OPTION(location()); }
plugin		{ return Parser::make_PLUGIN(location()); }
include		{ return Parser::make_INCLUDE(location()); }

TRUE {
  return Parser::make_BOOL(true, location());
}

FALSE {
  return Parser::make_BOOL(false, location());
}

NULL {
  return Parser::make_NONE(location());
}

 /* Dates. */
[0-9]{4}[\-/][0-9]{2}[\-/][0-9]{2} {
  // TODO(blais): Is it possible to add a lookahead symbol to prevent
  // "2013-12-228" from producing a valid date + integer?
  auto stdate = ParseDateFromString(matcher().begin());
  if (!stdate.ok()) {
    return Parser::make_YYerror(std::string(stdate.status().message()), location());
  }
  return Parser::make_DATE(stdate.value(), location());
}

 /* Account names. */
{ACCOUNTTYPE}(:{ACCOUNTNAME})+ {
  return Parser::make_ACCOUNT(str(), location());
}

 /* Currencies. These are defined as uppercase only in order to
  * disambiguate the syntax. This is to be kept in sync with
  * beancount.core.amount.CURRENCY_RE. */
[A-Z][A-Z0-9\'\.\_\-]{0,22}[A-Z0-9] {
  return Parser::make_CURRENCY(str(), location());
}

 /* String literals. */
\"([^\\\"]|\\.)*\" {
  // Check for very long strings and issue an error. This helps narrow down
  // errors when an unterminated string occurs.
  if (matcher().size() >= 16384) {
    return Parser::make_YYerror("String too long", location());
  }

  // TODO(blais): Add state to the scanner for specific encoding.
  std::string_view contents(matcher().begin() + 1, matcher().size() - 2);
  std::string unescaped;
  std::string error_message;
  if (!absl::CUnescape(contents, &unescaped, &error_message)) {
    return Parser::make_YYerror(error_message, location());
  }

  return Parser::make_STRING(std::move(unescaped), location());
}

 /* Numbers. */
([0-9]+|[0-9][0-9,]+[0-9])(\.[0-9]*)? {
  // Strip commas from the local buffer (without allocating memory).
  static char buffer[1024];
  if (size() < 1024) {
    CopyNoCommas(matcher().begin(), buffer, matcher().size());
  }
  decimal::Decimal number(buffer);
  return Parser::make_NUMBER(number, location());
}

 /* Tags. */
#[A-Za-z0-9\-_/.]+ {
  std::string text = {matcher().begin() + 1, matcher().size() - 1};
  return Parser::make_TAG(std::move(text), location());
}

 /* Links. */
\^[A-Za-z0-9\-_/.]+ {
  std::string text = {matcher().begin() + 1, matcher().size() - 1};
  return Parser::make_LINK(std::move(text), location());
}

 /* Keys. */
[a-z][a-zA-Z0-9\-_]+/: {
  return Parser::make_KEY(str(), location());
}

 /* Lines starting with an asterisk, a colon, an hash, or a character
  * in the FLAGS characters set are ignored. This rule is inserted
  * here to give higher precedence to rules matching valid tokens. */
^[\*\:\#]/.	{ start(IGNORE); }
^{FLAGS}/.	{ start(IGNORE); }

 /* Default rule. {bf253a29a820} */
. {
  matcher().unput(*text());
  start(INVALID);
}

<<EOF>> {
  /* Ensure location data is populated. */
  // TODO(blais): Review this, how to handle in RE/flex.
  /// YY_USER_ACTION;
  return Parser::make_YYEOF(location());
}

 /* Invalid input: skip over to to the next whitespace character. */
<INVALID>[^ \t\n\r]+ {
  // Create an error message to bubble up to the parser.
  std::ostringstream oss;
  oss << "Invalid token: '" << str() << "'";
  std::string error_message = oss.str();

  // Go back to initial state.
  start(INITIAL);

  // Return special error token generated by Bison.
  //
  // TODO(blais): I'd LOVE to be able to attach the error message for the error
  // to be handled in Bison here!
  return Parser::make_YYerror(error_message, location());
}
 /* Ignore input till the newline. */
<IGNORE>.* {
    start(INITIAL);
}


%%

//-----------------------------------------------------------------------------
// Code: Scanner Body

namespace beancount {
namespace scanner {

void CopyNoCommas(const char* src, char* buffer, size_t num_chars) {
  char* dst = buffer;
  for (int i = 0; i < num_chars; ++i, ++src) {
    if (*src == ',')
      continue;
    *dst++ = *src;
  }
  *dst = '\0';
}

namespace {

// // TODO(blais): Use the version from Abseil, which is probably faster.
//
// Convert ASCII string to an integer. Converts the 'string' string of length
// 'len' to int. The input is assumed to be a valid representation of an integer
// number. No input validation or error checking is performed.
int strtonl(const char* string, size_t len) {
    int result = 0;
    for (size_t i = 0; i < len; ++i) {
        result *= 10;
        result += string[i] - '0';
    }
    return result;
}

}  // namespace

#define DIGITS "0123456789"

absl::StatusOr<absl::CivilDay> ParseDateFromString(const char* string) {
  // Build date, parsing from fixed locations.
  int year = strtonl(string, 4);
  int month = strtonl(string + 5, 2);
  int day = strtonl(string + 8, 2);
  auto date = absl::CivilDay{year, month, day};

  // Detect invalid values out of range.
  if (date.year() != year || date.month() != month || date.day() != day) {
    return absl::InvalidArgumentError(
        absl::StrCat("Invalid date: '", std::string_view(string, 10), "'"));
  }

  return date;
}

absl::CivilSecond ParseTimeFromString(const char* string) {
  size_t n = strspn(string, DIGITS);
  int hour = strtonl(string, n);
  string += n + 1;

  n = strspn(string, DIGITS);
  int minute = strtonl(string, n);

  int second;
  if (*string == '\0') {
    second = 0;
  } else {
    string += n + 1;
    n = strspn(string, DIGITS);
    second = strtonl(string, n);
  }

  // TODO(blais): Handle failure to create here, e.g., with 26:00:00
  return absl::CivilSecond{hour, minute, second};
}

// Have another go at inserting time after date.
// TODO(blais): bring this back, it seems to make parsing signicantly slower.
// Two ideas: Move below for precedence, or include in date token with
// separator token.
 /* Times. */
 /*
  [0-9]{4,}[\-/][0-9]+[\-/][0-9]+\.([0-2][0-9]:[0-5][0-9]:([0-5][0-9])?)? {
     return Parser::make_TIME(ParseTimeFromString(text()), location());
  }
  %token <absl::CivilSecond> TIME   A time object
  */

}  // namespace scanner
}  // namespace beancount
